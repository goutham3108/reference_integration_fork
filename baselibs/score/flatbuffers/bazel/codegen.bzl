# *******************************************************************************
# Copyright (c) 2026 Contributors to the Eclipse Foundation
#
# See the NOTICE file(s) distributed with this work for additional
# information regarding copyright ownership.
#
# This program and the accompanying materials are made available under the
# terms of the Apache License Version 2.0 which is available at
# https://www.apache.org/licenses/LICENSE-2.0
#
# SPDX-License-Identifier: Apache-2.0
# *******************************************************************************

"""Starlark rules for generating source files from FlatBuffer schemas using flatc."""

def _generate_cpp_impl(ctx):
    """Implementation of the generate_cpp rule."""

    # Input .fbs file
    fbs_file = ctx.file.schema

    # Get the flatc compiler using absolute path from flatbuffers repository
    flatc = ctx.executable._flatc

    # Collect include directories from included .fbs files
    include_files = ctx.files.includes + [ctx.file._buffer_version_fbs]
    include_dirs = {files.dirname: True for files in include_files}

    # flatc generates <basename>_generated.h by default
    # Use temporary subdirectory based on target to avoid conflicts
    default_name = fbs_file.basename.replace(".fbs", "_generated.h")
    temp_subdir = "tmp_{}".format(ctx.label.name)
    generated_file = ctx.actions.declare_file("{}/{}".format(temp_subdir, default_name))
    out_header = ctx.actions.declare_file(ctx.attr.output)

    # Options for flatc --cpp: Generate a C++ header from a FlatBuffer schema.
    # Options that apply only to other modes are not listed.
    # flatc reference: https://flatbuffers.dev/flatc/
    #
    # Options considered and their decisions:
    #
    # --cpp (REQUIRED)
    #   Generate C++ code from FlatBuffer schema
    #
    # --cpp-std c++11 (EXPLICITLY SET TO C++11)
    #   Use C++11 code generation. Alternatives: c++0x (legacy), c++17 (experimental)
    #   DECISION: c++11 explicitly set to avoid experimental features and ensure reproducible builds
    #
    # --scoped-enums (ENABLED)
    #   Use C++11 style scoped enums (enum class). Adds type safety.
    #   DECISION: ENABLED by default - improves type safety by preventing accidental enum value
    #   collisions, enforces explicit enum scope qualification, and prevents implicit integer
    #   conversions.
    #
    # --gen-mutable (OUT OF SCOPE)
    #   Generate non-const accessors for in-place mutation of FlatBuffers.
    #   DECISION: Out of scope for current use case - module configuration
    #   Mutation support is usefull for storage and payload format use case.
    #   Allow optional activation once these shall be supported.
    #
    # --gen-object-api (OUT OF SCOPE)
    #   Generate object-based API for convenient construction/mutation.
    #   DECISION: Out of scope for current use case - module configuration
    #   This option relies on heap allocation for object construction, which conflicts with
    #   potential future heap-free buffer building requirements in safety-critical systems.
    #
    # --gen-name-strings (NOT USED)
    #   Generate GetFullyQualifiedName() methods for table structs (e.g., "my_component.demo.MyComponentConfig").
    #   DECISION: Not used - provides table type name reflection which is not needed for read-only buffer access.
    #
    # --gen-all (NOT USED)
    #   Generate code for all included schema files, not just current one.
    #   DECISION: Not used - this rule generates C++ headers only for the specified schema.
    #   Multi-schema code generation is out of scope; use separate rule instances for dependencies.
    #
    # --no-includes (NOT USED)
    #   Don't generate include statements for included schemas.
    #   DECISION: Not used - suppressing the #include directives does NOT inline or generate
    #   the dependent types.  The generated code still references types from included schemas
    #   (e.g. score::flatbuffers::BufferVersion) so the dependency must be satisfied by the
    #   consumer anyway, just without the generated header's guidance.
    #
    # --cpp-include (NOT USED)
    #   Add custom #include in generated file (e.g., --cpp-include "my_custom_include.h").
    #   DECISION: Not used - can be added once necessary

    args = ctx.actions.args()
    args.add("--cpp")
    args.add("--cpp-std", "c++11")
    args.add("--scoped-enums")
    for inc_dir in include_dirs:
        args.add("-I", inc_dir)
    args.add("-o", generated_file.dirname)
    args.add(fbs_file.path)

    ctx.actions.run(
        inputs = [fbs_file] + include_files,
        outputs = [generated_file],
        executable = flatc,
        arguments = [args],
        mnemonic = "FlatbuffersCompile",
        progress_message = "Generating C++ header from %s" % fbs_file.short_path,
    )

    # Symlink to the requested output name
    ctx.actions.symlink(output = out_header, target_file = generated_file)

    return [DefaultInfo(files = depset([out_header]))]

generate_cpp = rule(
    implementation = _generate_cpp_impl,
    attrs = {
        "schema": attr.label(
            allow_single_file = [".fbs"],
            mandatory = True,
            doc = "The .fbs FlatBuffer schema file to compile",
        ),
        "output": attr.string(
            mandatory = True,
            doc = "The name of the generated C++ header file",
        ),
        "includes": attr.label_list(
            allow_files = [".fbs"],
            default = [],
            doc = "Additional .fbs files required to resolve include directives in the schema.",
        ),
        "_flatc": attr.label(
            default = "@flatbuffers//:flatc",
            executable = True,
            cfg = "exec",
            doc = "The flatc compiler (absolute path from flatbuffers repository)",
        ),
        "_buffer_version_fbs": attr.label(
            default = "@score_baselibs//score/flatbuffers/common:buffer_version.fbs",
            allow_single_file = [".fbs"],
            doc = "Automatically included buffer_version.fbs for common buffer version support.",
        ),
    },
    doc = """Generates a C++ header file from a FlatBuffer schema (.fbs) file.

    This rule uses the flatc compiler from the @flatbuffers repository with
    absolute paths, making it usable outside of this repository.

    @score_baselibs//score/flatbuffers/common:buffer_version.fbs is always included
    automatically. The schema must include buffer_version.fbs manually if it uses
    the common buffer version (e.g. `include "buffer_version.fbs";`).

    Included schema headers are NOT generated automatically
    -------------------------------------------------------
    flatc only generates a C++ header for the schema passed directly to it.
    Schemas referenced via `include` directives are used for type resolution at
    compile time but their corresponding `*_generated.h` headers are NOT emitted
    as side-effects of this rule.

    The generated header will still contain `#include "<dependency>_generated.h"`
    directives for every included schema.  Those headers must therefore be made
    available separately:

    * For `buffer_version.fbs` (the standard version envelope): the header is
      already available via
      `@score_baselibs//score/flatbuffers/common:buffer_version_generated_bare`,
      which is re-exported by `@score_baselibs//score/flatbuffers:flatbufferutils`.
      Any target that depends on `flatbufferutils` automatically inherits it and
      no extra dep is needed.

    * For any other included schema: create a separate `generate_cpp` target for
      that schema and add it to the `deps` of the consuming `cc_*` target.

    Example:
        generate_cpp(
            name = "demo_flatbuffer",
            schema = "demo.fbs",
            output = "demo_config.h",
            includes = ["some_other.fbs"],
        )
    """,
)
